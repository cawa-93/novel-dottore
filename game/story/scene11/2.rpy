label scene11_2:
    
    call day(_("День 17"))

    if dotore_mad > 0:
        call .dottore_mad
    else:
        call .dottore_not_mad
    
    
    jump end

label .dottore_mad:
    play music "<from 0 to 170>the end.mp3"
    scene bg laboratory
    show dottore lab

    camera:
        blur 50
        linear 3.0 blur 0

    DT "Плідно ці черви попрацювали. Я задоволений."
    GG "…д…"
    GG "…допоможіть…"
    show dottore lab smile 4
    """
    Тобі здається, що це жахливий сон.
    
    Але від сну цей світ відрізняється справжнім, неймовірним болем, який пронизує все твоє тіло.
    
    Палає кожна зруйнована кістка, кожен пошкоджений червами м’яз.
    
    Жахливо.
    
    …
    
    Ти несвідомо торкаєшся язиком ясен, де ще тиждень тому були зуби.
    
    І які протягом кількох днів кришились, випадали, ламались від найменшого на них навантаження.
    
    Ти погано пам’ятаєш, але здається, твої нігті просто сповзли з фаланг, як звичайні наліпки на шкірі.
    
    У твоє ліве око ніби насипали розпечене вугілля. Тобі здається, що кілька тварюк досі звиваються під повікою, намагаючись догризтись до самого мозку.
    
    …
    
    Ти хочеш, щоб цей біль закінчився.
    
    Ти хочеш, щоб цей жах закінчився.
    
    Ти хочеш, щоб хтось прибрав червів з твого тіла, адже ти зараз більше нагадуєш труп, який стає єдиним цілим з природою, ніж живу людину.
    
    Тобі хочеться пити, а ще – щоб Дотторе змінив брудне простирадло під тобою на чисте та сухе.
    
    І щоб він нарешті замовк.
    """
    show dottore lab
    DT "Хм… Але все одно, тут луска проростає, тут, тут…"
    DT "Не розумію, чи виїли вони корені лусочок."
    DT "Що думаєш?"

    menu:
        "…":
            $ False
        "Допоможіть":
            $ False
        "Пити":
            $ False
        "Чому ти це зробив?..":
            $ False

    show dottore lab smile 10
    DT "Треба завести собі асистента."
    DT "Найкращий спосіб отримати відповіді на всі питання – безпосередньо подивитись на ситуацію, так би мовити, зсередини."
    DT "Цікаво, як глибоко в організм проникли ці істоти."

    show dottore knife

    GG "…"
    GG "…зупи… зупинись"
    DT "Буде трошки боляче."
    play sound "wet.mp3"
    "Лезо ножа одним швидким рухом розсікає тонку шкіру живота, оголюючи твої внутрішні органи."

    show black with dissolve
    pause 1
    hide black with dissolve
    show blood 1

    GG "…кх…"
    GG "А-А-А-А-А-А!"
    GG "ЗУПИНИСЬ!"
    """
    Ти на кілька секунд втрачаєш можливість дихати, давлячись повітрям.
    """
    show dottore lab knife 1
    DT "Як багато в тобі крові."
    GG "БОЛЯЧЕ!"
    GG "..ТИ!.."
    DT "Спокійно."

    call .singular_action(_("БОЛЯЧЕ!")) 

    DT "Тихіше."
    
    play sound "wet.mp3"
    "Ще один професійний розріз." with Shake((0.5, 1.0, 0.5, 1.0), 1.5, dist=10)
    show blood 2
    
    "Кров рікою ллється на кушетку та підлогу, забарвлюючи все навколо в багряний."
    

    call action_screem

    DT "Ти диви, вони дійсно заповзли всередину."
    """
    Дотторе засовує руку між твоїми органами і дістає довгого черва, який весь цей час сидів там.
    """
    DT "Який красень, чи не так?"

    scene bg black
    show worm big
    with fade

    GG "Чому?"
    GG "ЗА ЩО?!"

    show black:
        alpha 0.0
        linear 10 alpha 1
    """
    В очах починає швидко темніти.

    Кінцівки німіють, ніби ти лежиш без руху кілька годин.

    І холодно.
    """
    DT "Я просто намагаюсь знайти ліки."

    show black with dissolve


    """
    Холодно.
    
    Дуже холодно.
    
    Але вже не боляче.
    """

    return


label .dottore_not_mad:
    scene bg black
    GG "М-м-м…"

    menu:
        "Відкрити очі":
            $ False

    play music "<from 0 to 170>the end.mp3"
    scene bg laboratory night
    with Dissolve(5)

    GG "…хто-небудь…"
    GG "…пити…"
    GG "…хочу пити…"
    
    camera:
        blur 0
        linear 1.5 blur 30
        linear 2.0 blur 0
    
    """
    Ти обережно повертаєш голову вбік, розминаючи затерплі м’язи.

    Кілька червів падають з обкусаної руки на кушетку, ліниві, сонні та ситі.

    Ти досі гидуєш ними, але вже не так сильно, як на початку. Виявляється, до всього можна звикнути.

    Твої руки та ноги досі прив’язані, а тканина, якою накритий твій стіл – брудна та мокра.
    """
    GG "…допоможіть…"
    GG "…я… не хочу тут бути…"
    GG "…заберіть мене…"

    $ was_wakeup_selected = False

    while was_wakeup_selected == False:
        menu:
            "Нічого не робити":
                GG "…як хочеться спати…"
            "Спробувати встати":
                $ was_wakeup_selected = True

    """
    Ти дивишся на ремені, якими Дотторе тиждень тому прив’язав тебе до кушетки.
    
    Можливо, це лише ілюзія, викликана постійним болем та відсутністю сну, проте тобі здається, що принаймні один зі шкіряних ременів зараз виглядає менш надійним, ніж раніше.
    
    Ти сіпаєш рукою і розумієш, що ці кайдани тебе більше не тримають: будучи нестерпно голодними, черви погризли і їх.
    
    Нарешті.
    
    Свобода.
    """

    call .singular_action(_("Сісти"))


    """
    Ти сідаєш, намагаючись якомога менше тривожити звільнену руку, яка досі болить від падіння на підлогу кілька днів тому, і дуже повільно звільняєш себе від інших ременів.

    Права рука.

    Ліва нога.

    Права нога.

    Скинути з себе червів.

    Спробувати заплакати.
    """
    
    camera:
        blur 0
        linear 1.5 blur 20
        linear 1.0 blur 0

    GG "Який жах…"
    """
    Твоє тіло нагадує стару погризену собаками ляльку, яку викинули на смітник багато років тому.

    Елеазар хаотично стирчить з-під шкіри шматками.

    Ноги синюваті, вивернуті у неприродньому положенні.

    Зуби…

    Ти язиком торкаєшся оголених ясен, де донедавна були міцні, здорові зуби.

    І які один за одним протягом кількох днів розкришились. Випали. Зламались, залишивши тебе мацати запалені криваві лунки, якими неможливо їсти навіть ту переварену юшку, яку тобі періодично приносив вчений.

    Зникли і твої нігті. Частину з них погризли черви, частина – просто відпала, розчинилась, зникла.

    …

    Ти голосно схлипуєш і обіймаєш себе.
    """
    GG "Я так не хочу… Не можу…"

    call .singular_action(_("Злізти з кушетки"))

    """
    Ти обережно спускаєш ступні на підлогу і намагаєшся встати, але твої ноги моментально підгинаються і ти падаєш на холодний бетон.
    """
    with Shake((0.5, 1.0, 0.5, 1.0), 1.5, dist=10)
    GG "…допоможіть…"
    GG "…я так більше не можу…"

    call .singular_action(_("НЕ МОЖУ"))

    "Ти оглядаєш знайоме тобі приміщення і помічаєш рішення всіх своїх проблем на одному із столів."
    hide window
    show half_black
    show knife
    with dissolve
    pause

    "Так близько і так далеко одночасно."
    
    
    call .singular_action(_("Повзти вперед"))

    """
    Ти починаєш рухатись. Черви, які впали зі столу додолу, не подають ознак життя.

    Добре.
    """

    call .singular_action(_("Так вам і треба"))

    """
    Стіл все ближче, та здається, у тебе зовсім не залишилось сил, щоб здолати ці п’ять метрів.
    """

    call .singular_action(_("Повзти вперед"))
    GG "…допоможіть…"
    call .singular_action(_("ПОВЗТИ ВПЕРЕД"))

    scene bg laboratory with dissolve
    """
    Тебе нудить від усього, що з тобою сталося і що потрібно буде зараз зробити.

    Чому?

    За що?

    Чи можна було щось змінити?
    """
    GG "…я не знаю…"
    GG "…але мені треба…"

    GG "…я…"
    GG "…потрібно…"

    call .what_to_do

    """
    Ти міцніше береш ножа до рук і притискаєш його до місця, де синіють ниточки вен.

    Мабуть, це треба десь отут, вірно?
    """

    call .singular_action(_("Порізати себе"))
    show blood 1
    GG "Ай!"
    GG "…"
    call .singular_action(_("Порізати себе"))

    show blood 2
    """
    Кров багряними краплями стікає на підлогу, збираючись у невелику калюжу.
    """
    call .singular_action(_("Порізати себе"))
    show blood 3

    GG "…чому так боляче…"
    """
    Тебе нудить, але ти з останніх сил тримаєшся.
    """
    call .singular_action(_("ПОРІЗАТИ СЕБЕ"))

    show black:
        alpha 0.0
        linear 10 alpha 1

    "Кров тонким струмочком стікає по твоїй блідій шкірі."

    "Ніж падає на підлогу і тихо дзеленчить, але ти вже майже не чуєш цього."

    """
    Тут так темно.
    І тихо.
    І холодно.
    І вже не боляче.
    """

    return


label .what_to_do(want_kill_dottore = True):
    menu:
        GG "…потрібно…"

        "Вбити Дотторе" if want_kill_dottore:
            GG "…я не зможу… я нічого не зможу йому зробити…"
            GG "…він знову зв’яже мене і продовжить катувати…"
            call .what_to_do(False)

        "Вбити себе":
            if was_letter_send:
                GG "…я сподіваюсь, ти виявишся розумнішим за мене і не потрапиш в його пастку…"
    return

label .singular_action(label):
    menu:
        "[label]":
            pass
    return