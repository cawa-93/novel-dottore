label scene10_2:
    
    call day(_("День 12"))

    play music "day-1.mp3"
    show bg laboratory night
    with dissolve

    show dottore lab smile 4
    with dissolve

    DT "Ну, як ти тут? Що нового? Як себе почуваєш?"


    menu:
        "Висваритись ":
            $ dotore_mad += +10
            DT "Шкода, що ти не хочеш співпрацювати. Тобі же гірше."
        "Розповісти про стан":
            $ dotore_mad += -10
            DT "Я щось таке і очікував. Зараз огляну тебе."

    play sound "metal.mp3"
    """
    Вчений нахиляється до тебе і ключем відкриває кайданки, якими прикував тебе до труби.
    """
    DT "Лягай на стіл."
    hide dottore
    """
    Вибір у тебе небагатий, тож ти покірно і дуже обережно встаєш з матраца.
    """
    camera:
        blur 0
        linear 1.0 blur 20
        linear 2.0 blur 0
    """
    Голова починає крутитись від довгого перебування у лежачому положенні і ти хапаєшся рукою за стіну, щоб не втратити рівновагу.
    """

    GG "Щось не так."
    GG "Суглоби болять."

    "Втім, Дотторе не звертає увагу на твої слова і чекає на своєму звичному місці біля стола."

    "Ти чекаєш кілька секунд, прислуховуючись до себе, і з особливою обережністю робиш крок вперед."

    GG "!!!"
    GG "Ой!"
    DT "Що таке?"

    play sound "bones churching.mp3"
    """
    Ти відчуваєш, як неприродньо хрускотить твоя ліва нога від коліна і нижче.
    
    І не тільки хрускотить.
    """
    play sound "bones crunching.wav"
    show bg laboratory night with Shake((0.5, 1.0, 0.5, 1.0), 1.5, dist=10)
    "В одну секунду земля іде з-під ніг і ти падаєш на підлогу посеред лабораторії." 
    
    play sound "<from 4 to 5>ouch.mp3" volume 1 
    GG "…"
    GG "Боляче!"
    GG "…м-м…"
    show dottore lab with dissolve
    DT "Не скигли. Вставай і не затримуй роботу."
    GG "Я не можу!"
    """
    Ти справді не можеш піднятись.
    
    Твоя нога та обидві руки від удару об холодний бетон почали боліти з неймовірною силою, змушуючи зосередитися тільки на власних відчуттях.
    
    Здається, всередині тебе розсипали скло, частинки якого дряпають тебе при найменшому русі.
    
    Вчений підходить до тебе і швидко оглядає, присівши поруч.
    """
    show dottore lab smile 9
    DT "От дідько. Здається, у тебе зламались кістки."
    GG "…щ… що?.. Як це?"
    show dottore lab smile 10
    DT "Препарат працює навіть краще, ніж я очікував."
    DT "Сподіваюсь, елеазар більше не виріс."
    DT "Зараз глянемо."

    show dottore lab smile 9
    show bg laboratory with dissolve
    """
    Дотторе підхоплює тебе на руки і дбайливо перекладає з підлоги на застелену тканиною кушетку.
    """
    "І як завше міцно прив’язує тебе набридлими брудними ременями."

    DT "Що у нас тут?"
    show dottore lab smile 4

    """
    Він швидко розв’язує бинти з однієї ноги, з силою віддираючи його з місць, де той прилипнув до ран, і нахиляється нижче.

    Ти стогнеш від болю.
    """

    DT "…"
    show dottore lab angry 2
    DT "Щоб тебе хілічурли зжерли! Він знову росте."
    DT "І навіть такий же міцний, як і до того."
    show dottore lab angry
    DT "Дивина та й годі."

    """
    Ти мовчки спостерігаєш, як вчений щось напружено обдумує.
    
    Кінцівки болять, але не так жахливо, як можна було подумати.
    
    Терпимо. Дивно.
    """
    show dottore lab
    DT "Що ж, не спрацювало. Давай пробувати щось інше."
    GG "Наприклад?"
    DT "Варіантів безліч. Але я пропоную ось що…"
    hide dottore
    "Дотторе відходить від тебе і щось шукає у своєму столі."
    
    "Ти чекаєш, сподіваючись, що це буде щось не таке травматичне, як раніше."
    show dottore lab smile 9

    DT "Не так давно один з моїх колег приніс мені цікавих істот."
    DT "Він довго спостерігав за їх поведінкою і виявив, що вони майже всеїдні."
    show dottore lab smile 5
    DT "Але ось що зацікавило мене: з усіх запропонованих, хм, страв, вони полюбляли їсти залишки трупів, які пролежали в лісах Сумеру більше кількох місяців."
    DT "Точніше сказати, вони полюбляли їсти саме їх кістки, копита та зуби."
    show dottore lab smile 8
    DT "Розумієш?"
    GG "…"
    GG "…"
    GG "…"
    GG "…ні…"
    DT "Шкода. Диви, які красені."

    hide window
    scene bg black
    show dottore worm
    with dissolve
    pause

    DT "Я відібрав найбільших та найактивніших особин."
    GG "…"

    scene bg laboratory
    show dottore lab smile 7

    GG "…ти… ти хочеш мене вбити?"
    DT "Звісно що ні! Я намагаюсь тебе вилікувати."
    show dottore lab
    GG "Яким чином?"
    DT "..."
    DT "Серйозно, ти не розумієш?"
    DT "..."
    DT "Ох… Добре, пояснюю на пальцях."
    show dottore lab smile 4
    play music "laboratory hello.mp3"
    DT "Я вважаю, що ці чудові створіння можуть повністю позбавити тебе від елеазару. Кілька днів вони харчуватимуться лусочками, аж поки не вигризуть їх до кореня."
    DT "Не буде основи лусочки – вона перестане рости, розумієш?"

    menu:
        "Я не хочу цих істот на своєму тілі!":
            $ dotore_mad += +10
        "Я боюсь червів":
            $ dotore_mad += -10

    show dottore lab smile 7
    DT "Це ненадовго і для твого ж блага."
    DT "Головне слідкуй, щоб вони не лізли глибоко під шкіру та не почали гризти кістки."
    GG "…"
    GG "ЩО?!"
    show dottore lab smile 4
    DT "Я не можу сидіти тут з тобою 24/7, тож доведеться тобі трохи поконтролювати процес."
    """
    Незважаючи на твій голосний протест, вчений дуже обережно садить гидку тварюку на твою ліву ногу.
    
    Ти з відразою спостерігаєш за червом, який спокійно лежить, не подаючи ознак життя.
    """
    show dottore lab smile 9
    DT "Давай, прокидайся."
    """
    Гострий писок черва підіймається вгору, ніби той почав щось винюхувати. Раптом гидке слизьке тіло різко стискається і тварюка починає швидко повзти до найближчої великої лусочки.
    """
    GG "Прибери її!"
    GG "ПРИБЕРИ!"
    show dottore lab angry
    DT "Не кричи, бо налякаєш."

    menu:
        "Забери його!":
            $ dotore_mad += +10
        "Замовкнути":
            $ dotore_mad += -10


    camera:
        blur 0
        linear 1.5 blur 30
        linear 2.0 blur 0
    """
    Ти відчуваєш, як черв маленькими зубами починає шкребти тверду лусочку елеазара.

    Ти гидливо здригаєшся, але черв тримається міцно.
    """
    show dottore lab smile 9
    DT "Давай трохи пришвидшимо процес."

    """
    Дотторе зненацька висипає на тебе цілий контейнер черв’яків різного розміру та довжини.
    """
    hide window
    scene bg black
    show legs worm
    with dissolve
    pause

    GG "ЗАБЕРИ ЇХ! ЗАБЕРИ! ЗАБЕРИ!"
    play sound "grass.mp3" volume 2.0

    "Черви розповзаються по твоєму тілу, обліплюючи зони з елеазаром. Ти гидливо смикаєшся, але біль від розтрощених кісток та ремені не дають тобі вскочити з кушетки."


    menu:
        "Прибери їх негайно!":
            $ dotore_mad += +10
        "Спробувати заспокоїтись":
            $ dotore_mad += -10


    hide window
    scene bg black
    show legs worm 2
    with dissolve
    pause

    """
    Ти відчуваєш кожну тварюку, яка кусає тебе за лусочки.

    Вони слизькі, мерзенні, огидні, повзають по тобі і ти нічого не можеш з цим зробити.

    А що як одна з них заповзе у вухо чи ніс?!
    """

    DT "Та не пищи ти, у мене голова починає боліти."

    menu:
        "Висваритись":
            $ dotore_mad += +10
        "Будь ласка, досить":
            $ dotore_mad += -10

    play sound "grass.mp3" volume 2.0
    """
    Вони скрізь.

    Вони повзають.

    Вони відкушують елеазар по шматочкам, іноді чіпляючи зубами запалену шкіру.
    """
    GG "…"
    GG "Вони лізуть під шкіру…"
    DT "Тобі здається."
    GG "ВОНИ ЛІЗУТЬ ВСЕРЕДИНУ! ЗАБЕРИ ЇХ!"
    DT "Значить, їм так треба. Потерпи трохи."
    play sound "<from 0 to 4>wet-crunching.mp3"
    show blood 1 with dissolve
    GG "Мене зараз знудить. Вони гидкі!"
    DT "Ти мене стомлюєш. Я ж не змушую тебе їх їсти. Потерпи кілька днів, хай виконають свою роботу."

    hide window
    scene bg black
    show legs worm 3
    with dissolve
    pause
    play sound "grass.mp3" volume 2.0
    """
    Ти не розумієш, правда це, чи сон.

    Тобі здається, що під шкірою повзають черви, ніби ти якийсь небіжчик.

    Тобі здається, що вони вигризають шкіру, щоб дістатись до кісток ніг та рук.

    Чи не здається?
    """
    hide legs worm 3
    with dissolve
    show bg laboratory
    with dissolve
    show dottore lab
    with dissolve

    GG "...будь ласка…"
    DT "Приберу їх трохи пізніше. А поки – лежи спокійно."
    """
    Вчений ще раз задоволено оглядає всю картину.

    Ти тамуєш подих і закриваєш очі, сподіваючись, що Дотторе таки зглянеться і забере цих монстрів.
    """
    hide dottore
    with dissolve
    """
    Проте ти чуєш, як чоловік відходить від тебе, а через кілька секунд – скрип вхідних дверей.
    """
    stop music
    play sound "key2.mp3"
    GG "ГЕЙ!"
    GG "ТИ КУДИ?!"
    GG "Не залишай мене з ними!"
    GG "ЧУЄШ МЕНЕ?!"
    """
    Навряд.

    Єдині, хто тебе зараз чує – це купа червів на твоєму тілі.

    Ти відчуваєш, як кільки особин починають повзти доверху вздовж живота.
    """

    "Ти опускаєш очі."
    play sound "day 1 634645__m-murray__insidious-1.mp3"
    hide window
    show half_black
    show worm big
    pause
    "Схоже, на всіх них елеазару не вистачить."
    "І не всі вони харчуються виключно кальцієм."

    scene bg black with dissolve

    jump scene11_2