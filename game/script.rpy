# У цьому файлі міститься скрипт гри.

# Оголошення символів, які використовуються в цій грі. Колірний аргумент розфарбовує
# ім'я персонажа.

define GG = Character("player_name", dynamic=True)
define DT = Character(name="Дотторе", image="dottore")
define O = Character(name="Охоронець", image="guardian")
define T = Character(name="Трактирник", image="tavernkeeper")



# Гра починається тут.

label start:

    call scene1
    call scene2
    call scene3






    "PAUSE FOR DEBUG"

    return

label scene1:
    
    scene bg city entrance

    """
    Ти полегшено зітхаєш і пришвидшуєш крок.
    
    Ну нарешті!{w} Кілька тижнів снів під відкритим небом у компанії хмари комарів, десятки кілометрів не найбезпечнішими ділянками лісу, харчування самими лише ягодами та засушеним м’ясом, і ось ти тут – у місті Сумеру!
    
    Аж не віриться. Ти щипаєш себе за руку і, зойкнувши, розумієш, що все навколо реальне.
    
    Що ж, чудово! Залишилось найпростіше: потрапити у місто, повечеряти та знайти Академію.
    
    А ось і вхід.
    """

    show guardian
    O "Вітаю! Прошу назвати ваше ім’я та ціль прибуття у місто"

    python:
        player_name = renpy.input("Представитись", length=32)
        player_name = player_name.strip()


    # TODO: Унікальні відповіді для кожного варіанта
    menu:
        "Кажуть, що в Академії знайшли ліки від елеазару. Хочу перевірити їх на собі":
            O "Зрозуміло, [GG]. Спитайте детальніше про це у трактирі, туди стікаються всі новини"
        "Підлікуватись та відпочити у гарному місці":
            O "Зрозуміло, [GG]. Спитайте детальніше про це у трактирі, туди стікаються всі новини"
        "Шукаю одного лікаря, який живе тут":
            O "Зрозуміло, [GG]. Спитайте детальніше про це у трактирі, туди стікаються всі новини"

    O """
    Також маю повідомити, що останнім часом почастішали звернення від родичів мандрівників про щезнення людей. 
    
    Тож якщо випили – ідіть відразу у номер готелю, а не бігайте по незнайомим вуличкам у пошуках пригод.

    Та я впевнений, ніякої проблеми немає.
    """

    GG "..."

    O """
    Сумеру – дуже безпечне місто, а ті мандрівники продовжили шлях і зникли десь у незнайомих для них лісах.
    
    Не турбуйтесь!
    
    Гарного вам перебування!
    """

    GG "Дякую!"

    return

label scene2:
    scene bg city sumeru

    pause

    "Ти ідеш по місту роззираючись навколо. Сонце в зеніті і добряче потилицю, тому ти вирішуєш накинути капюшон, щоб сховатись від нього"

    show bg city sumeru market

    "Ти потрапляєш на вуличку, на якій розташувалась велика кількість крамничок з фруктами, овочами, місцевим одягом та сувенірами. Навколо них скупчилось багато туристів, тому ти ідеш далі, щоб не штовхатись у натовпі."

    show bg city sumeru flower

    "Біля дороги росте дуже багато дерев та рослин, більшість з яких тобі не знайомі. Ти з цікавістю нахиляєшся, щоб понюхати незвичайну красиву квітку з прозорими пелюстками і зі здивуванням виявляєш, що та реагує на твоє наближення нервовим поворотом стебла. Тобі стає не по собі через відчуття, ніби вона за тобою стежить, тому ти вирішуєш не чіпати її та йдеш собі геть"

    show bg city sumeru students

    "Повз тебе іноді проходять люди в однаковому зеленому одязі і ти робиш висновок, що це студенти Академії, в яку обов’язково потрібно потрапити до настання сутінок. Можливо, тобі навіть дозволять оселитись у студентських кімнатах, що було би дуже доречно, так як на свою подорож ти витрачаєш майже всі власні кошти."


    show bg city sumeru industrial_quarter

    """
    Ти повільно доходиш до кварталу, де працюють гончарі, ковалі, пекарі та навіть кілька випускників Академії, які, здається, щось розповідають про зірки та астрономію всім охочим послухати. Тут дуже шумно і тісно від людей, діти грають у квача та хованки.
    
    Тобі постійно доводиться підійматись угору, що швидко виснажує і так стомлене тіло, від постійного крику починає боліти голова, а запах свіжого хліба нагадує, що настав час поїсти. Тому ти без вагань запитуєш у перехожого, де знаходиться трактир.
    """

    return

label scene3:
    scene bg tavern

    show tavernkeeper
    T "Вітаю! Займайте будь-який вільний стіл, більшість відвідувачів вже пообідали, тому місця вистачає."

    call scene3_tavern_menu

    T "За п’ять хвилим подам."

    call scene3_tavern_drinks

    T "Очікуйте."

    scene bg tavern table


    """
    Ти сідаєш за стіл на двох, який знаходиться на другому поверсі таверни, подалі від чужих очей та розмов.
    
    Дійсно, за кілька хвилин тобі приносять (назва обраної страви) та (назва обраного напою). Ти з радістю відмічаєш, що незнайома страва дуже смачна, тому ти замовляєш ще одну порцію, якою можна повільно насолоджуватись найближчу годину.
    
    Так як ніхто не бачить тебе, ти обережно підіймаєш рукав плаща та розглядаєш тверді лусочки, які знову з’явились під час загострення хвороби
    """

    show hand eleazor

    pause

    """
    Пальці погано гнуться та тремтять, а рани під лусочками – болять так сильно, що хочеться плакати. Ти приречено чухаєш в одну нігтем і розчаровано розумієш, що нарости стали більш твердими у порівнянні з тими, якими ти їх запам’ятала перед початком своєї подорожі.

    Одна надія на те, що вчені з Академії дійсно знайшли ліки від цієї хвороби. Ти сподіваєшся, що лікування не займе багато часу, адже вдома тебе з нетерпінням чекає мама та маленький брат, якого потрібно щодня доглядати.

    Елеазар став для тебе справжнім покаранням.

    Ти більше не можеш досхочу гуляти зі своїми друзями, ходити з ними до Високого Озера на ночівлі та разом шукати сезонні ягоди в глибині лісу.

    Не можеш їсти улюблений падісаровий пудинг, адже після нього лусочки болять ще сильніше, а шкіра навколо них тріскає та свербить.

    Не можеш допомагати мамі по господарству так майстерно і швидко, як ти це робила ще кілька років тому.

    Ти не хочеш стати тягарем для своєї родини і тому ти зараз тут – у місті Сумеру, щоб попросити одного з вчених Академії про допомогу.
    """

    DT "Вітаю!"

    hide hand
    show dottore smile
    with fade

    GG "Ой!"

    "Ти так поринаєш у свої думки, що навіть не помічаєш, як до тебе підходить незнайомий чоловік у масці. Яка цікава мода в цьому місті."

    DT "Не лякайся. У мене невелика перерва в роботі, тож я вирішив спуститись у трактир пообідати. Випадково побачив у тебе на руках елеазар і вирішив підійти."

    menu ():
        DT "Тут не зайнято?"

        "Прошу":
            $ False
        "Мій друг зараз прийде":
            DT "Хм… Дійсно? Тоді почекаю його разом з тобою, якщо ти не проти."

    # false_choice(["fff"])

    DT "Хочеш фокус? Я вмію читати думки"

    GG "Ніхто цього не вміє"

    DT "Що ж, готовий посперечатися на 1000 мори, що я все ж маю певні здібності."

    DT "Хм…"

    DT "Ти живеш не у цьому місті, а подорожуєш здалеку?"

    DT "До тебе долетіла новина, що в Академії ось-ось знайдуть ліки від елеазару і ти хочеш стати піддослідною мишкою?"

    GG "Мишкою?"

    GG "..."

    DT "Робота зобов’язує бути таким"

    DT "Розслабся"

    DT "Мене звати Дотторе і це я шукаю ліки від елеазару. Маю в цьому напрямі деякий прогрес, тому зараз мені потрібні пацієнти, які готові допомогти у експериментах. Ти ж за цим тут?"
    
    
    """
    Ти збентежено кліпаєш очима та ствердно киваєш головою. Що це: удача, чи архонти намагаються пошвидше спекатись тебе?
    
    Дотторе на хвилину відходить і повертається вже з двома чашками в руках.
    """
    
    # FIXME: Чаїнки
    DT "Сподіваюсь, тобі сподобається. (з’являється зображення чаїнок). Це мій найкращий збір, тримаю для особливих гостей. (зникає зображення чаїнок)"

    "Ти обережно відсьорбуєш чай, який тобі подав Дотторе. Незважаючи на те, що він досить гарячий, напій тобі до смаку і ти продовжуєш його пити, періодично дмухаючи над поверхнею."

    DT "Отже, розкажи мені, як давно ти хворієш?"


    menu:
        "Кілька місяців":
            DT "Недовго. Значить, є більше шансів, що ліки швидко подіють і ти через тиждень підеш додому."
        "Кілька років":
            DT "Добре, що ти тут. Обіцяю, я докладу всіх зусиль, щоб змінити твоє життя."

    DT "Пропоную надовго не відкладати і розпочати роботу вже сьогодні. Боюсь, час грає проти нас, тому треба розібратись з цим якнайшвидше."

    GG "Погоджуюсь. Можна поставити одне запитання?"

    DT "Звісно."

    menu:
        "Це боляче?":
            DT "Скоріше так, аніж ні. Але обіцяю, що після того, як я з тобою попрацюю, хвороба тебе більше не турбуватиме."
        "Це довго?":
            DT "Якщо робити висновок з твоїх симптомів, я думаю, знадобиться кілька тижнів. Деяких пацієнтів я відпускав через кілька днів після початку лікування, деяких  – через "
        "Чи хтось вже вилікувався?":
            DT "Так. Принаймні, хвороба їх більше не хвилювала."

    DT "Тепер моя черга питати. Елеазаром у родині хворієш тільки ти?"

    GG "Так. Брат та мама не заражені"

    DT """
    Не був би я таким впевненим. Дуже часто діти хворіють разом, тільки у одного елеазар проявляється раніше, а у другого – пізніше, іноді навіть через кілька років.
    
    Якщо симптомів ще нема, я радив би все одно перевірити його стан. Вилікувати елеазар до перших лусочок на тілі можливо одним щепленням, тоді як у твоєму випадку може знадобитись більше часу.
    
    Можемо відправити брату листа, щоб він прибув сюди і я його зміг оглянути. Якщо все буде добре – підете додому разом, або залишитесь ще на кілька днів.

    В Сумеру скоро відбудеться свято Прийдення і я впевнений, що вам буде цікаво там побувати. Щороку на головній площі розгортається величезний ярмарок, приїжджають степовики, студенти влаштовують наукові шоу та проводять змагання. Буде купа смаколиків, сувенірів та цікавинок, тож я вважаю, що це буде чудове закінчення твого лікування.
    """

    "Його слова звучать як щось неможливе. Ви з братом і мріяти не могли побувати на Прийденні, а цей чоловік ще й пропонує вирішити всі твої проблеми наче помахом магічної палички"

    GG "Що я маю дати взамін? У нас не так багато грошей, якщо казати відверто"

    DT smile large "Тільки взяти участь у моєму експерименті. Це все." 

    GG "І все?"

    DT "Так. Гроші на дослідження видає Академія, кімнатою та харчуванням вона також забезпечить."

    DT "Найкраща нагорода для лікаря – здоровий пацієнт."

    menu: 
        "Ти вагаєшся. Що робити?"

        "Написати листа":
            call scene4_with_letter

        "Не писати листа":
            call scene4_without_letter


    return



label scene3_tavern_menu:
    menu: 
        T "Чого бажаєте?"

        "Ароматні м’ясні кульки":
            return
        "Грибне асорті":
            return
        "Тропічний салат":
            return
        "Курча в маслі":
            return
        "Піту":
            return

label scene3_tavern_drinks:
    menu: 
        T "Що будете пити?"

        "Чай":
            return
        "Сік":
            return
        "Молоко":
            return
        "Вино":
            return

label scene4_with_letter:
    """
    Ти вирішуєш довіритись вченому і на під його наглядом пишеш невеликого листа брату, щоб той прибув у місто якомога швидше. 
    
    Ти залишаєш детальні інструкції, де можна буде знайти з Дотторе, якщо по якійсь причині ти не зможеш його зустріти.
    
    Вчений навпроти тебе начебто нікуди не спішить, п’є чай і лише періодично вносить якісь уточнення в текст.
    """

    DT "Готово? Дай мені, я закину його у поштову скриньку, а ти поки іди в Академію. Знайдеш там секретаря і попросиш заселити тебе у кімнату. Завтра зранку я зайду за тобою і ми почнемо лікування, якщо ти не передумаєш, звісно."

    DT "А, ще згадав. Постарайся мінімально контактувати зі студентами та викладачами. Багато з них дуже упереджено ставляться до хворих елеазаром, тому не варто їх нервувати."

    """
    Ти киваєш головою і віддаєш листа вченому. Той прощається з тобою і наостанок детально розповідає, як дійти до Академії та де саме можна знайти секретаря.
    
    Дотторе каже, що зайде по тебе завтра зранку і просить нікуди не йти, особливо в пошуках сніданку. Ти з усім погоджуєшся і виходиш з таверни слідом за лікарем.
    """

    scene bg tavern exit
    """
    Сонце вже почало сідати за горизонт. 
    
    Ти прощаєшся з Дотторе і спішиш піднятись по вказаній дорозі до Академії, щоб нарешті вирішити всі запитання та лягти спати раніше.
    """

    scene bg room

    GG "Нарешті я можу відпочити"

    """
    Ти без проблем заселяєшся у житло. Секретар не задавав зайвих запитань і після одного погляду на твої руки з лусочками без коментарів віддав ключі від кімнати.
    
    Що ж, цей день був довгим і як тільки ти сідаєш на стільчик, твої очі самі собою починають закриватись. Більшість вчених відпочиває або готується до завтрашніх лекцій/досліджень, тож і ти вирішуєш готуватись до сну.
    """

    scene shower

    """
    Змиваючи пил та пісок зі шкіри, ти мимо волі думаєш про сім’ю, яка залишилась вдома.
    Цікаво, як швидко доставлять твого листа? Чи зможе мама привести брата у місто? А раптом у нього теж елеазар?
    Ти починаєш нервувати, але швидко заспокоюєш себе. Навряд чи брат також захворів, а якщо так, то лікар Дотторе зможе йому допомогти.
    Мабуть.
    Від води лусочки на тілі починають пекти ще більше і ти спішиш вимкнути воду та витерти тіло м’яким рушником. (звук води всьо) Тріщини на шкірі знову почали сочитися кров’ю.
    Потрібно лягати спати. Завтра точно буде краще.
    """

    scene room dark

    GG "Що за…"

    """
    Ти точно пам’ятаєш, що десять хвилин тому світло у кімнаті було увімкнене.

    Ти нервово шукаєш перемикач, але в темряві не можеш намацати його.
    """

    GG "Спокійно. Все добре. Мабуть, воно вимикається само собою."

    "Так, ці розумні вчені точно зробили світло, що вимикається самостійно. Точно."

    "Боятися нема чого."

    GG "Та де ж ти?"

    GG """
    Га?"

    Тут хтось є?
    """

    show dottore shadow

    GG """
    
    …
    
    Хто тут?
    
    …
    
    Агов?
    
    Мабуть, у мене галюцинації…

    """


    show dottore smile crazy

    DT "Вибач, не зміг чекати до ранку"

    scene black with fade
    pause
    

    return



label scene4_without_letter:
    "Ти не хочеш писати листа. Зрештою, якби у брата чи мами був елеазар, це окрім тебе точно хтось помітив би."

    GG " Дякую, проте це не потрібно."

    DT neutral "Діло твоє"

    "Дотторе уважно роздивляється твої руки, але більше нічого не каже. Ти стискаєшся і намагаєшся приховати лусочки під рукавами, проте тобі це не дуже вдається"


    DT "Я думаю, ми можемо вже сьогодні почати лікування. Пішли до мене в лабораторію, дам тобі першу дозу ліків, щоб не затягувати."

    "Ви допиваєте чай і покидаєте трактир."

    scene bg street dark

    DT "Завжди бісило в цьому Сумеру те, що тут так рано темніє"

    GG "Хіба це рано?"

    DT "Так. На моїй батьківщині сонце встає о сьомій ранку і сідає о дев’ятій вечора."

    "Ви ідете по закрученим дорогам міста. Вулиці майже пусті, лише деінде стоять стражі порядку. Дотторе іноді кивком голови з кимось вітається, але іде мовчки, думаючи про своє."

    menu: 
        "Поговорити про лікування":
            GG "Як буде проходити лікування? В сенсі, це пігулки, чи мазі якісь?"
            DT "Трошки того, трошки іншого. Потім розповім, бо в цих вулиць є дуже цікаві вуха, які дуже хочуть внести своє ім’я в підручники, але не бажають навіть пальцем ворухнути для цього."
        "Спитати про дослідження":
            GG "Як довго ти цим займаєшся? Чому так довго ніхто не міг знайти ліки від елеазару?"
            DT "Близько десяти років, але цікавився темою з дитинства. Елеазар важко досліджувати, адже мало хто погоджується стати піддослідним кроликом, щоб допомогти лікарям знайти чарівну пігулку від хвороби"
            GG "..."
            GG "Мені не подобається порівняння з кроликом"
            DT smile "Нікому не подобається"

    DT "Майже на місці"



    """
    Сонце зовсім зайшло за горизонт і стало набагато прохолодніше. Ти кутаєшся в дорожній плащ та непомітно для вченого позіхаєш, прикривши рота долонею. Довго дорога сильно втомила тебе, а ситна вечеря – в кінець розморила.

    Здається, ви з Дотторе вже вийшли за місто. Навколо немає ні будинків, ні людей, ні вуличних тварин, тільки голосніше співають цвіркуни. Ти починаєш нервуватись і думаєш, чи не варто тобі повернути назад. Адже ще не пізно звернутись в Академію чи пошукати хостел на цю ніч.

    """

    DT "Ось і прийшли"

    "Дотторе відчиняє масивні ворота і ви разом заходите у двір невеликого будинку, який сховався у високих густих сумерських заростях. Вчений провертає ключ і пропускає тебе у темний будинок"
    
    scene bg corridor dark

    menu: 
        "Агов?":
            $ False
        "Можна увімкнути світло?":
            $ False

    DT "Зліва від тебе сходи вниз. Спускайся, там є світло."

    GG "Ем…"

    default menuset = set()

    label downsteirs:
        menu:
            set menuset
            "Спуститись":
                call scene4_without_letter_basement
            "Повернути назад":
                "Ти робиш крок назад, але натикаєшся на Дотторе, який стоїть відразу за тобою"
                DT "Чого ти? Іди вперед"
                "Тобі нічого не залишається як підкоритись"
                call downsteirs

    
    return

label scene4_without_letter_basement:

    scene bg basement downsteirs

    "Ти обережно ступаєш по сходах. Схоже, це якесь підвальне приміщення, яке дійсно підсвічується слабкими вогниками вздовж стіни, тож ти із лікарем без перешкод опиняєтесь внизу"

    scene bg laboratory dark

    GG """

    Що?

    Гей! Не потрібно мене зачиняти!

    А?..
    """

    GG "Що відбувається?"


    scene laboratory

    GG "..."

    GG "Де я?"

    DT "А на що схоже?"


    DT "Будемо лікуватись?"
    GG "…"
    GG "Ні."
    DT "В будь-якому разі вже темно, повертатись назад пізно."
    DT "Кажуть, у місті люди ночами…"
    extend "…пропадають…"

    "У тебе починає крутитись голова. Ноги терпнуть, руки трясуться настільки, що ти навіть не можеш схопитись за невеликий ніж на поясі, яким ти зазвичай користуєшся у походах."

    DT "Хороший чай, правда? Головне правильно розрахувати час."

    GG "Я… мене шукатимуть."

    DT "Хай шукають."
    DT "Можливо, навіть знайдуть. Я не проти."
    DT "А тепер пора спати."

    scene black with fade
    pause

    DT "Нас чекає цікаве дослідження."

    return
